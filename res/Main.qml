import QtQuick 2.15
import QtQuick.Controls 2.15
import QtQuick.Layouts 1.15

import Application.UI 1.0
import Application.Core 1.0
import Theme 1.0
import Components.Custom 1.0
import Components.Crypto 1.0

// Главное окно
ApplicationWindow {

    id: mainWindow
    visible: true
    width: 1000
    height: 800
    title: "EasyTrade"
    color: Theme.windowColor

    visibility: "FullScreen"

    Connections {
        target: AppCore.marketService

        //function onErrorOccurred(error) { statusWidget.text = error }

        function onMessageReceived(msg) { statusWidget.text = msg }

        function onDownloadProgress(received, total) {
            if (total > 0)
                statusWidget.progressValue = received / total * 100
        }
    }

    Component.onCompleted: {
        // Инициализируем ядро приложения
        AppCore.init()
    }

    // Меню
    menuBar: CustomMenuBar {
        menuModel: [
            {
                text: "Services",
                items: [
                    {
                        text: "User",
                        clicked: function() { mainStack.showUserScreen() }
                    },
                    {
                        text: "Trade",
                        clicked: function() {
                            mainStack.showTradeScreen()
                        }
                    },
                    {
                        text: "Available trades",
                        clicked: function() {
                            selecterTradeWindowLoader.active = true
                        }
                    }

                ]
            },
            {
                text: "Repository",
                items: [
                    {
                        text: "Load",
                        items: [
                            {
                                text: "Spot",
                                clicked: function() { AppCore.marketService.loadTradePairs(Tools.MarketType.Spot) }
                            },
                            {
                                text: "Linear",
                                clicked: function() { AppCore.marketService.loadTradePairs(Tools.MarketType.Linear) }
                            },
                            {
                                text: "Inverse",
                                clicked: function() { AppCore.marketService.loadTradePairs(Tools.MarketType.Inverse) }
                            },
                            {
                                text: "Option",
                                clicked: function() { AppCore.marketService.loadTradePairs(Tools.MarketType.Option) }
                            }
                        ]
                    },
                    { text: "---" },
                    {
                        text: "Export",
                        items: [
                            {
                                text: "Klines"
                            }
                        ]
                    },
                ]
            },
            {
                text: "View",
                items: [
                    {
                        text: "Order book",
                        clicked: function() { mainStack.currentItem.showOrderbook() }
                    },
                    {
                        text: "Recent trades",
                        clicked: function() { mainStack.currentItem.showRecentTrades() }
                    },
                    {
                        text: "Volume chart",
                        clicked: function() { mainStack.currentItem.enableVolumeChart = !mainStack.currentItem.enableVolumeChart }
                    }
                ]
            },
            {
                text: "Settings",
                clicked: function() { settingsWindowLoader.active = true }
            }
        ]
    }

    // Главное окно
    Page {
        id: mainPage
        anchors.fill: parent

        background: Rectangle { color: Theme.windowColor }

        header: CustomToolBar {
            modelToolButtons: [
                { id: "btn_run", icon: "qrc:/icons/icon_play.png" },
                { id: "btn_restart", icon: "qrc:/icons/icon_restart.png" },
                { id: "btn_stop", icon: "qrc:/icons/icon_stop.png" }
            ]
            onToolButtonClicked: function(id, name) {
                if (id === "btn_run")
                    AppCore.marketService.run()
                else if (id === "btn_restart")
                    AppCore.marketService.restart()
                else if (id === "btn_stop")
                    AppCore.marketService.shutdown()
            }
        }

        StackView {
            id: mainStack
            anchors.fill: parent

            initialItem: UserScreen {}

            //property bool isTradeScreen: mainStack.currentItem instanceof TradeScreen
            //property bool isUserScreen: mainStack.currentItem instanceof UserScreen

            // Метод для вызова экрана торговли
            function showTradeScreen()
            {
                mainStack.replace("Views/TradeScreen.qml")
            }

            // Метод для вызова экрана пользователя
            function showUserScreen()
            {
                mainStack.replace("Views/UserScreen.qml")
            }

        } // mainStack

        footer: Rectangle {
            color: Theme.toolBarColor
            height: 30

            RowLayout {
                anchors.fill: parent

                CustomStatusBar {
                    id: statusWidget
                    Layout.fillHeight: true
                    Layout.fillWidth: true
                    autoHide: false
                    //visibleProgressBar: false
                }

                Item { Layout.fillWidth: true }

                PingIndicator {
                    width: 30
                    height: 30
                    thickness: 1
                    pingValue: AppCore.marketState.pingMs
                }
            }
        }

    } // mainLayout

    Loader {
        id: settingsWindowLoader
        active: false
        source: "Views/SettingsWindow.qml"

        Connections {
            target: settingsWindowLoader.item
            function onVisibleChanged() {
                if (settingsWindowLoader.item && !settingsWindowLoader.item.visible)
                    settingsWindowLoader.active = false
            }
        }
    }

    Loader {
        id: selecterTradeWindowLoader
        active: false
        source: "Views/SelecterTradeWindow.qml"

        onLoaded: {
            item.tradeSelected.connect(function(symbol) {
                AppCore.marketService.subscribeSymbol(symbol)
            })
        }

        Connections {
            target: selecterTradeWindowLoader.item
            ignoreUnknownSignals: true

            function onVisibleChanged() {
                if (selecterTradeWindowLoader.item && !selecterTradeWindowLoader.item.visible)
                    selecterTradeWindowLoader.active = false
            }
        }
    }

}
